//Noro
//2018

//@version=3
strategy(title = "Robot WhiteBox ShiftMA2", shorttitle = "Robot WhiteBox ShiftMA2", overlay = true, default_qty_type = strategy.percent_of_equity, default_qty_value = 100, pyramiding = 0)

//Settings
needlong = input(true, defval = true, title = "Long")
needshort = input(true, defval = true, title = "Short")
capital = input(100, defval = 100, minval = 1, maxval = 10000, title = "Lot")
per = input(3, title = "MA Length")
matype = input(defval = "SMA", type = string, options = ["ALMA", "SMA", "EMA", "VWMA", "WMA"], title = "MA type")
s = input(defval = "7. OHLC4", type = string, options = ["1. Open", "2. High", "3. Low", "4. Close", "5. HL2", "6. HLC3", "7. OHLC4", "8. OC2", "9. PCMA"], title = "MA Source")
tf = input(defval = "0. Current", type = string, options = ["0. Current", "1. 15 min", "2. 30 min", "3. 1 hour", "4. 2 hour", "5. 4 hour", "6. 6 hour", "7. 12 hour", "8. 1 day"], title = "MA Timeframe")
shortlevel = input(10.0, title = "Short line, %")
longlevel = input(-5.0, title = "Long line, %")
cs = input(0.0, defval = 0.0, title = "close shift, %")
arrows = input(false, defval = false, title = "Show arrows")
fromyear = input(1900, defval = 1900, minval = 1900, maxval = 2100, title = "From Year")
toyear = input(2100, defval = 2100, minval = 1900, maxval = 2100, title = "To Year")
frommonth = input(01, defval = 01, minval = 01, maxval = 12, title = "From Month")
tomonth = input(12, defval = 12, minval = 01, maxval = 12, title = "To Month")
fromday = input(01, defval = 01, minval = 01, maxval = 31, title = "From day")
today = input(31, defval = 31, minval = 01, maxval = 31, title = "To day")

mult = 1 / syminfo.mintick

//Trading
size = strategy.position_size
lot = 0.0
lot := size == 0 ? strategy.equity / close * capital / 100 : lot[1]

bar = close > open ? 1 : close < open ? -1 : 0

//MA
alttf = tf == "0. Current" ? period : tf == "1. 15 min" ? '15' : tf == "2. 30 min" ? '30' : tf == "3. 1 hour" ? '60' : tf == "4. 2 hour" ? '120' : tf == "5. 4 hour" ? '240' : tf == "6. 6 hour" ? '360' : tf == "7. 12 hour" ? '720' : '1440'
oc2 = (open + close) / 2
src = s == "1. Open" ? open : s == "2. High" ? high : s == "3. Low" ? low : s == "4. Close" ? close : s == "5. HL2" ? hl2 : s == "6. HLC3" ? hlc3 : s == "7. OHLC4" ? ohlc4 : s == "8. OC2" ? oc2: close
pcma = security(tickerid, alttf, (highest(high, per) + lowest(low, per)) / 2)
ma = s == "9. PCMA" ? round(pcma * mult) / mult : round(security(tickerid, alttf, matype == "EMA" ? ema(src, per) : matype == "WMA" ? wma(src, per) : matype == "ALMA" ? alma(src, per, 0.85, 6) : matype == "VWMA" ? vwma(src, per) : sma(src, per)) * mult) / mult
shortline = needshort ? round(ma * ((100 + shortlevel) / 100) * mult) / mult : close
longline = needlong ? round(ma * ((100 + longlevel) / 100) * mult) / mult : close
cslong = ma - ((ma / 100) * cs)
csshort = ma + ((ma / 100) * cs)

//Lines
macolor = ma[1] > 0 ? blue : na
shortcolor = ma[1] > 0 and needshort ? red : na
longcolor = ma[1] > 0 and needlong ? lime : na
closeshortcolor = ma > 0 and needshort and cs != 0 ? red : na
closelongcolor = ma > 0 and needlong and cs != 0 ? lime : na
plot(shortline, linewidth = 2, color = shortcolor, title = "Short line")
plot(csshort, linewidth = 2, color = closeshortcolor, title = "Close short line", style = circles)
plot(ma, linewidth = 2, color = macolor, title = "MA line")
plot(cslong, linewidth = 2, color = closelongcolor, title = "Close long line", style = circles)
plot(longline, linewidth = 2, color = longcolor, title = "Long line")

//Arrows
longsignal = false
shortsignal = false
longsignal := low <= longline[1] and size[1] == 0
shortsignal := high >= shortline[1] and size[1] == 0
plotarrow(longsignal and arrows and needlong ? 1 : na, colorup = blue, colordown = blue, transp = 0)
plotarrow(shortsignal and arrows and needshort ? -1 : na, colorup = blue, colordown = blue, transp = 0)

//Orders
if (not na(close[per])) and needlong and size == 0
    strategy.entry("Long", strategy.long, lot, limit = longline, when = (time > timestamp(fromyear, frommonth, fromday, 00, 00) and time < timestamp(toyear, tomonth, today, 23, 59)))
    
if (not na(close[per])) and needshort and size == 0
    strategy.entry("Short", strategy.short, lot, limit = shortline, when = (time > timestamp(fromyear, frommonth, fromday, 00, 00) and time < timestamp(toyear, tomonth, today, 23, 59)))
    
if (not na(close[per])) and size > 0 
    strategy.entry("Close", strategy.short, 0, limit = cslong, when = (time > timestamp(fromyear, frommonth, fromday, 00, 00) and time < timestamp(toyear, tomonth, today, 23, 59)))
    
if (not na(close[per])) and size < 0 
    strategy.entry("Close", strategy.long, 0, limit = csshort, when = (time > timestamp(fromyear, frommonth, fromday, 00, 00) and time < timestamp(toyear, tomonth, today, 23, 59)))

if time > timestamp(toyear, tomonth, today, 23, 59)
    strategy.close_all()