// (c) Noro
//2017

study(title="Noro's SILA v1.2", shorttitle="SILA v1.2", overlay=true)
sens = input(1, title="Sensivity", minval = 1, maxval = 5)
dist = input(5, title="Distance", minval = 1, maxval = 20)
usewow = input(true, title="Use WOW-indicator?")
usebma = input(true, title="Use BestMA-indicator?")
usebc = input(true, title="Use BarColor-indicator?")
usest = input(true, title="Use SuperTrend-indicator?")
usedi = input(true, title="Use DI-indicator?")
usetl = input(true, title="Need trendlines?")
usebg = input(true, title="Need background?")
usearr = input(true, title="Need arrows?")

// WOW 1.0 method
len = 30
lasthigh = highest(close, len)
lastlow = lowest(close, len)
center = (lasthigh +lastlow) / 2
body = (open + close) / 2
trend1 = body > center ? 1 : body < center ? -1 : trend1[1]
trend2 = center > center[1] ? 1 : center < center[1] ? -1 : trend2[1]
WOWtrend = usewow == true ? trend1 == 1 and trend2 == 1 ? 1 : trend1 == -1 and trend2 == -1 ? -1 : WOWtrend[1] : 0

// BestMA 1.0 method
SMAOpen = sma(open, len)
SMAClose = sma(close, len)
BMAtrend = usebma == true ? SMAClose > SMAOpen ? 1 : SMAClose < SMAOpen ? -1 : BMAtrend[1] : 0

// BarColor 1.0 method
color = close > open ? 1 : 0
score = color + color[1] + color[2] + color[3] + color[4] + color[5] + color[6] + color[7]
BARtrend = usebc == true ? score > 5 ? 1 : score < 3 ? -1 : BARtrend[1] : 0

// SuperTrend mehtod
Up = hl2 - (7 * atr(3))
Dn = hl2 + (7 * atr(3))
TrendUp = close[1] > TrendUp[1] ? max(Up, TrendUp[1]) : Up
TrendDown = close[1] < TrendDown[1] ? min(Dn, TrendDown[1]) : Dn
SUPtrend = usest == true ? close > TrendDown[1] ? 1: close < TrendUp[1]? -1 : SUPtrend[1] : 0

//DI method
th = 20
TrueRange = max(max(high-low, abs(high-nz(close[1]))), abs(low-nz(close[1])))
DirectionalMovementPlus = high-nz(high[1]) > nz(low[1])-low ? max(high-nz(high[1]), 0): 0
DirectionalMovementMinus = nz(low[1])-low > high-nz(high[1]) ? max(nz(low[1])-low, 0): 0
SmoothedTrueRange = nz(SmoothedTrueRange[1]) - (nz(SmoothedTrueRange[1])/14) + TrueRange
SmoothedDirectionalMovementPlus = nz(SmoothedDirectionalMovementPlus[1]) - (nz(SmoothedDirectionalMovementPlus[1])/14) + DirectionalMovementPlus
SmoothedDirectionalMovementMinus = nz(SmoothedDirectionalMovementMinus[1]) - (nz(SmoothedDirectionalMovementMinus[1])/14) + DirectionalMovementMinus
DIPlus = SmoothedDirectionalMovementPlus / SmoothedTrueRange * 100
DIMinus = SmoothedDirectionalMovementMinus / SmoothedTrueRange * 100
DItrend = usedi == true ? DIPlus > DIMinus ? 1 : -1 : 0

//plots
trends = WOWtrend + BMAtrend + BARtrend + SUPtrend + DItrend
price = sma(close, 5)
per = dist / 100
color1 = usetl == true ? trends > 0 ? blue : na : na
plot(price * (1 - per), color=color1, linewidth=1, title="Lower line 1")
color2 = usetl == true ? trends > 1 ? blue : na : na
plot(price * (1 - 2 * per), color=color2, linewidth=1, title="Lower line 2")
color3 = usetl == true ? trends > 2 ? blue : na : na
plot(price * (1 - 3 * per), color=color2, linewidth=1, title="Lower line 3")
color4 = usetl == true ? trends > 3 ? blue : na : na
plot(price * (1 - 4 * per), color=color4, linewidth=1, title="Lower line 4")
color5 = usetl == true ? trends > 4 ? blue : na : na
plot(price * (1 - 5 * per), color=color5, linewidth=1, title="Lower line 5")
color6 = usetl == true ? trends < 0 ? blue : na : na
plot(price * (1 + per), color=color6, linewidth=1, title="Upper line 1")
color7 = usetl == true ? trends < -1 ? blue : na : na
plot(price * (1 + 2 * per), color=color7, linewidth=1, title="Upper line 2")
color8 = usetl == true ? trends < -2 ? blue : na : na
plot(price * (1 + 3 * per), color=color8, linewidth=1, title="Upper line 3")
color9 = usetl == true ? trends < -3 ? blue : na : na
plot(price * (1 + 4 * per), color=color9, linewidth=1, title="Upper line 4")
color10 = usetl == true ? trends < -4 ? blue : na : na
plot(price * (1 + 5 * per), color=color10, linewidth=1, title="Upper line 5")

//background
bgcolor = usebg == true ? trends >= sens ? lime : trends <= (-1 * sens) ? red : na : na
bgcolor(bgcolor, transp=70)

//arrows
pos = trends >= sens ? 1 : trends <= (-1 * sens) ? -1 : pos[1]
arr = usearr == true ? pos == 1 and pos[1] < 1 ? 1 : pos == -1 and pos[1] > -1 ? -1 : na : na
plotarrow(arr == 1 ? 1 : na, title="UpArrow", colorup=blue, maxheight=60, minheight=50, transp=0)
plotarrow(arr == -1 ? -1 : na, title="DnArrow", colordown=blue, maxheight=100, minheight=50, transp=0)
