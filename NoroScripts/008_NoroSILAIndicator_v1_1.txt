// (c) Noro
//2017

study(title="Noro's SILA v1.1", shorttitle="SILA v1.1", overlay=true)

// WOW 1.0 method
len = 30
lasthigh = highest(close, len)
lastlow = lowest(close, len)
center = (lasthigh +lastlow) / 2
body = (open + close) / 2
trend1 = body > center ? 1 : body < center ? -1 : trend1[1]
trend2 = center > center[1] ? 1 : center < center[1] ? -1 : trend2[1]
WOWtrend = trend1 == 1 and trend2 == 1 ? 1 : trend1 == -1 and trend2 == -1 ? -1 : WOWtrend[1]

// BestMA 1.0 method
SMAOpen = sma(open, len)
SMAClose = sma(close, len)
BMAtrend = SMAClose > SMAOpen ? 1 : SMAClose < SMAOpen ? -1 : BMAtrend[1]

// BarColor 1.0 method
color = close > open ? 1 : 0
score = color + color[1] + color[2] + color[3] + color[4] + color[5] + color[6] + color[7]
BARtrend = score > 5 ? 1 : score < 3 ? -1 : BARtrend[1]

// SuperTrend mehtod
Up=hl2-(7*atr(3))
Dn=hl2+(7*atr(3))
TrendUp=close[1]>TrendUp[1]? max(Up,TrendUp[1]) : Up
TrendDown=close[1]<TrendDown[1]? min(Dn,TrendDown[1]) : Dn
SUPtrend = close > TrendDown[1] ? 1: close < TrendUp[1]? -1 : SUPtrend[1]

//DI method
th = 20
TrueRange = max(max(high-low, abs(high-nz(close[1]))), abs(low-nz(close[1])))
DirectionalMovementPlus = high-nz(high[1]) > nz(low[1])-low ? max(high-nz(high[1]), 0): 0
DirectionalMovementMinus = nz(low[1])-low > high-nz(high[1]) ? max(nz(low[1])-low, 0): 0
SmoothedTrueRange = nz(SmoothedTrueRange[1]) - (nz(SmoothedTrueRange[1])/14) + TrueRange
SmoothedDirectionalMovementPlus = nz(SmoothedDirectionalMovementPlus[1]) - (nz(SmoothedDirectionalMovementPlus[1])/14) + DirectionalMovementPlus
SmoothedDirectionalMovementMinus = nz(SmoothedDirectionalMovementMinus[1]) - (nz(SmoothedDirectionalMovementMinus[1])/14) + DirectionalMovementMinus
DIPlus = SmoothedDirectionalMovementPlus / SmoothedTrueRange * 100
DIMinus = SmoothedDirectionalMovementMinus / SmoothedTrueRange * 100
DItrend = DIPlus > DIMinus ? 1 : -1

//plots
trends = WOWtrend + BMAtrend + BARtrend + SUPtrend + DItrend
price = sma(close, 10)
color1 = trends > 0 ? blue : na
plot(price * 0.95, color=color1, linewidth=1, title="Lower line 1")
color2 = trends > 1 ? blue : na
plot(price * 0.90, color=color2, linewidth=1, title="Lower line 2")
color3 = trends > 2 ? blue : na
plot(price * 0.85, color=color2, linewidth=1, title="Lower line 3")
color4 = trends > 3 ? blue : na
plot(price * 0.80, color=color4, linewidth=1, title="Lower line 4")
color5 = trends > 4 ? blue : na
plot(price * 0.75, color=color5, linewidth=1, title="Lower line 5")
color6 = trends < 0 ? blue : na
plot(price * 1.05, color=color6, linewidth=1, title="Upper line 1")
color7 = trends < -1 ? blue : na
plot(price * 1.10, color=color7, linewidth=1, title="Upper line 2")
color8 = trends < -2 ? blue : na
plot(price * 1.15, color=color8, linewidth=1, title="Upper line 3")
color9 = trends < -3 ? blue : na
plot(price * 1.20, color=color9, linewidth=1, title="Upper line 4")
color10 = trends < -4 ? blue : na
plot(price * 1.25, color=color10, linewidth=1, title="Upper line 5")

//background
bgcolor(trends > 1 ? lime : trends < -1 ? red : na, transp=70)