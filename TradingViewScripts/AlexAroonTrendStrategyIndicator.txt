//@version=3 
study(title="Alex Aroon Trend Strategy Indicator", overlay=true) 

////////////////////////////////////////////////////////////////////////////////
// **************** Alex Aroon Trend Strategy Indicator - v0.2 *********************
// - Initial implementation
// - Implemented Stop Loss
//////////////////////////////////////////////////////////////////////////////// 

openPositionSize = 0.0 
openPositionSize := nz(openPositionSize[1], 0.0)
maxCurrentProfitPct = 0.0 
maxCurrentProfitPct := nz(maxCurrentProfitPct[1], 0.0)
isTrailingTakeProfitActivated = false
isTrailingTakeProfitActivated := nz(isTrailingTakeProfitActivated[1], false)
avgOpenPositionValue = 0.0 
avgOpenPositionValue := nz(avgOpenPositionValue[1], 0.0)
trailingTakeProfitDistance = 0.0
trailingTakeProfitDistance := nz(trailingTakeProfitDistance[1], 0.0)
stopLossValue = 0.0 
stopLossValue := nz(stopLossValue[1], 0.0)

commissionValue = input(title="Full Trade Commission Value, %", type=float, defval=0.4)
stopLossEnabled = input(title="Stop-Loss Enabled", type=bool, defval=true)
minStopLossValue = input(title="Min Stop-Loss Value, %", type=float, defval=2.0)
maxStopLossValue = input(title="Max Stop-Loss Value, %", type=float, defval=5.0)
startTrailingTakeProfitTarget = input(title="Take Profit Trailing Start Target, %", type=float, defval=3.5)
startTrailingTakeProfitTarget2 = input(title="Take Profit Trailing Start Target-2, %", type=float, defval=7.5)

isLongPositionOpen() =>
    openPositionSize > 0

isShortPositionOpen() =>
    openPositionSize < 0 

calculateCurrentProfitPct() => 
    iff(isLongPositionOpen(), (close - avgOpenPositionValue) * 100 / avgOpenPositionValue, iff(isShortPositionOpen(), (avgOpenPositionValue - close) * 100 / avgOpenPositionValue, 0.0))

isTrailingTakeProfitCloseCondition() =>
    c = calculateCurrentProfitPct()
    iff(isTrailingTakeProfitActivated == true and c > 0 and (maxCurrentProfitPct - c) > trailingTakeProfitDistance, true, false)

getActualStopLoss() =>
    stopLossValue - commissionValue
    
isStopLossCondition(isLongPositionType) =>
    if(isLongPositionType == true)
        iff(stopLossEnabled == true and openPositionSize == 1 and close < avgOpenPositionValue and ((avgOpenPositionValue - close)*100/close) >= getActualStopLoss(), true, false)
    else
        iff(stopLossEnabled == true and openPositionSize == -1 and close > avgOpenPositionValue and ((close - avgOpenPositionValue)*100/close) >= getActualStopLoss(), true, false)

inRange(value, minV, maxV) =>
    value >= minV and value <= maxV

fastEMAValue = 55
slowEMAValue = 200
fastEMA = ema(close, fastEMAValue)
slowEMA = ema(close, slowEMAValue)
length = input(slowEMAValue, minval=1)
upper = 100 * (highestbars(high, length+1) + length)/length
lower = 100 * (lowestbars(low, length+1) + length)/length
__stdev = stdev(close, slowEMAValue)
__stdevOHLC4 = stdev(ohlc4, slowEMAValue)

calculateDynamicStopLossPct() =>
    v = __stdev * 100 / close
    r = iff(v < minStopLossValue, minStopLossValue, iff(v >= minStopLossValue and v <= maxStopLossValue, v, maxStopLossValue))
    r

calculateDynamicTakePrifitDistancePct() =>
    2 * __stdevOHLC4 * 100 / ohlc4

dsl = calculateDynamicStopLossPct()
currentProfitPct = calculateCurrentProfitPct()
isTrailingTakeProfitActivated := iff(not isTrailingTakeProfitActivated and currentProfitPct >= startTrailingTakeProfitTarget, true, isTrailingTakeProfitActivated)
maxCurrentProfitPct := iff(isTrailingTakeProfitActivated and currentProfitPct > 0 and currentProfitPct > maxCurrentProfitPct, currentProfitPct, maxCurrentProfitPct)
newDistance = calculateDynamicTakePrifitDistancePct()
trailingTakeProfitDistance := iff(maxCurrentProfitPct > maxCurrentProfitPct[1], newDistance, trailingTakeProfitDistance)

openLongCondition = iff(openPositionSize == 0 and crossover(upper[1], lower[1]) and inRange(lower[1], 20,  80) and inRange(upper, 90, 100), true, false)
openShortCondition = iff(openPositionSize == 0 and crossover(lower[1], upper[1]) and inRange(upper[1], 20,  80) and inRange(lower, 90, 100), true, false)

if(openLongCondition == true)
    openPositionSize := 1
    avgOpenPositionValue := close
    stopLossValue := calculateDynamicStopLossPct()

if(openShortCondition == true)
    openPositionSize := -1
    avgOpenPositionValue := close
    stopLossValue := calculateDynamicStopLossPct() 

closeLongCondition = iff(openPositionSize == 1 and (isTrailingTakeProfitCloseCondition() or isStopLossCondition(true) or crossover(lower, upper)), true, false)
closeShortCondition = iff(openPositionSize == -1 and (isTrailingTakeProfitCloseCondition() or isStopLossCondition(false) or crossover(upper, lower)), true, false)

c = iff(closeLongCondition == true or closeShortCondition == true, -1 * openPositionSize, 0)
o = iff(openLongCondition == true, 1, iff(openShortCondition == true, -1, 0))
plotarrow(c, offset=0, colorup=#29fd2fff, colordown=#fffd38ff, maxheight=150, minheight=130, transp=0)
plotarrow(o, colorup=#29fd2fff, colordown=#fffd38ff, maxheight=150, minheight=130, transp=0)

alertcondition(openLongCondition == true,  title='0201 - Aroon Trend Strategy Alert - Signal to Open Long Position', message='Aroon Trend Strategy - Signal to Open Long Position!')
alertcondition(openShortCondition == true, title='0202 - Aroon Trend Strategy Alert - Signal to Open Short Position', message='Aroon Trend Strategy - Signal to Open Short Position!')
alertcondition((closeLongCondition == true or closeShortCondition == true), title='0203 - Aroon Trend Strategy Alert - Signal to Close Position', message='Aroon Trend Strategy - Signal to Close Position!')

if(closeLongCondition == true or closeShortCondition == true)
    openPositionSize := 0
    maxCurrentProfitPct := 0.0
    isTrailingTakeProfitActivated := false
    trailingTakeProfitDistance := 0.0
    avgOpenPositionValue := 0.0 
    stopLossValue := 0.0

plot(0, color=black)


