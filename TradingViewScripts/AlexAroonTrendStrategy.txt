//@version=3
strategy("Alex Aroon Trend Strategy", calc_on_order_fills=false, overlay=true, initial_capital=1, commission_type=strategy.commission.percent, commission_value=0.1)

////////////////////////////////////////////////////////////////////////////////
// **************** Alex Aroon Trend Strategy - v0.1 *********************
// - Initial implementation
//////////////////////////////////////////////////////////////////////////////// 

openPositionSize = 0.0 
openPositionSize := nz(openPositionSize[1], 0.0)
maxCurrentProfitPct = 0.0 
maxCurrentProfitPct := nz(maxCurrentProfitPct[1], 0.0)
isTrailingTakeProfitActivated = false
isTrailingTakeProfitActivated := nz(isTrailingTakeProfitActivated[1], false)
avgOpenPositionValue = 0.0 
avgOpenPositionValue := nz(avgOpenPositionValue[1], 0.0)
trailingTakeProfitDistance = 0.0
trailingTakeProfitDistance := nz(trailingTakeProfitDistance[1], 0.0)

startTrailingTakeProfitTarget = input(title="Take Profit Trailing Start Target, %", type=float, defval=3.5)
startTrailingTakeProfitTarget2 = input(title="Take Profit Trailing Start Target-2, %", type=float, defval=7.5)
defaultTrailingTakeProfitDistance = input(title="Default Trailing Take Profit Distance, %", type=float, defval=1.5)

isLongPositionOpen() =>
    openPositionSize > 0

isShortPositionOpen() =>
    openPositionSize < 0 

calculateCurrentProfitPct() => 
    iff(isLongPositionOpen(), (close - avgOpenPositionValue) * 100 / avgOpenPositionValue, iff(isShortPositionOpen(), (avgOpenPositionValue - close) * 100 / avgOpenPositionValue, 0.0))

isTrailingTakeProfitCloseCondition() =>
    c = calculateCurrentProfitPct()
    iff(isTrailingTakeProfitActivated == true and c > 0 and (maxCurrentProfitPct - c) > trailingTakeProfitDistance, true, false)

inRange(value, minV, maxV) =>
    value >= minV and value <= maxV

fastEMAValue = 55
slowEMAValue = 200
fastEMA = ema(close, fastEMAValue)
slowEMA = ema(close, slowEMAValue)
length = input(slowEMAValue, minval=1)
upper = 100 * (highestbars(high, length+1) + length)/length
lower = 100 * (lowestbars(low, length+1) + length)/length

currentProfitPct = calculateCurrentProfitPct()
isTrailingTakeProfitActivated := iff(not isTrailingTakeProfitActivated and currentProfitPct >= startTrailingTakeProfitTarget, true, isTrailingTakeProfitActivated)
maxCurrentProfitPct := iff(isTrailingTakeProfitActivated and currentProfitPct > 0 and currentProfitPct > maxCurrentProfitPct, currentProfitPct, maxCurrentProfitPct)
newDistance = 2 * stdev(ohlc4, 14) * 100 / ohlc4
trailingTakeProfitDistance := iff(maxCurrentProfitPct > maxCurrentProfitPct[1], newDistance, trailingTakeProfitDistance)

openLongCondition = iff(openPositionSize == 0 and crossover(upper[1], lower[1]) and inRange(lower[1], 20,  80) and inRange(upper, 90, 100), true, false)
openShortCondition = iff(openPositionSize == 0 and crossover(lower[1], upper[1]) and inRange(upper[1], 20,  80) and inRange(lower, 90, 100), true, false)
closeLongCondition = iff(openPositionSize == 1 and (isTrailingTakeProfitCloseCondition() or crossover(lower, upper)), true, false)
closeShortCondition = iff(openPositionSize == -1 and (isTrailingTakeProfitCloseCondition() or crossover(upper, lower)), true, false)

if (time > timestamp(2018,1,1,0,0)) and time < timestamp(2018,9,1,0,0)
    strategy.close("Long", when = openPositionSize == 1 and closeLongCondition == true)
if (time > timestamp(2018,1,1,0,0)) and time < timestamp(2018,9,1,0,0)
    strategy.close("Short", when = openPositionSize == -1 and closeShortCondition == true)
if (time > timestamp(2018,1,1,0,0)) and time < timestamp(2018,9,1,0,0)
    strategy.entry("Long", strategy.long,   when = openPositionSize == 0 and openLongCondition == true)
if (time > timestamp(2018,1,1,0,0)) and time < timestamp(2018,9,1,0,0)
    strategy.entry("Short", strategy.short, when = openPositionSize == 0 and openShortCondition == true)

//bbb = iff(isTrailingTakeProfitActivated, 1, 0)
// ccc = iff(isTrailingTakeProfitCloseCondition(), 1, 0)
//plotchar(0.000084+bbb*0.0000005, color=#ffff00ff, location=location.absolute, char='o')
// plotchar(0.000084+ccc*0.0000005, color=#ff0000ff, location=location.absolute, char='o')
//plotchar(iff(trailingTakeProfitDistance > 0, 0.000084+trailingTakeProfitDistance*close/100, 0), color=#ffffffff, location=location.absolute, char='o')

if(openLongCondition == true)
    openPositionSize := 1
    avgOpenPositionValue := close

if(openShortCondition == true)
    openPositionSize := -1
    avgOpenPositionValue := close
   
if(closeLongCondition == true or closeShortCondition == true)
    openPositionSize := 0
    maxCurrentProfitPct := 0.0
    isTrailingTakeProfitActivated := false
    avgOpenPositionValue := 0.0 
    trailingTakeProfitDistance := 0.0

